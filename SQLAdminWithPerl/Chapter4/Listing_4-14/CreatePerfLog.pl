# This script takes the perfmon counter values exported to a file
# from SQL Server and convert them to the Perfmon CSV file.

# Essentilly, this script does the reserver of the script 
# preparePerfLog.pl in Listing 4-12

use strict;
use SQLDBA::Utility qw( dbaSetDiff );

my $timeColumnHeader = '(PDH-CSV 4.0) (Eastern Daylight Time)(240)';
my $sampleDate;
my $sampleCount = 1;
my $rowCount = 0;
my @counters;
my @theCounters;
my @values;

my $file = shift or die "***Err: Expects a tab-separated file.\n";
open(CNTR, "$file") or die "***Err: Could not open the data file $file.\n";

while(<CNTR>) {
   ++$rowCount;
   chomp;
   my ($rowDate, $server, $object, $instance, $counter, $value) = split (/\t/);
   
   # Some counter paths include an instance component, and others don't
   my $counterPath = $instance ? 
                        "\\\\$server\\$object($instance)\\$counter":
                        "\\\\$server\\$object\\$counter";
                        
   # the very first row is special
   if ($rowCount == 1) {   
      $sampleDate  = $rowDate;   
      @values      = ($value);
      @counters    = ($counterPath);
      @theCounters = ($counterPath);
   }
   # Now take care of the other rows
   else {
      # the new row is still in the same sample
      if ($sampleDate eq $rowDate) { 
         push @values, $value;
         push @counters, $counterPath;
         push @theCounters, $counterPath if $sampleCount == 1;
      }
      # the new row is from a different sample, i.e we are crossing 
      # the sample boundary
      else { 
         printHeader($timeColumnHeader, @theCounters) if $sampleCount == 1;

         if (dbaSetDiff(\@counters, \@theCounters) or 
             dbaSetDiff(\@theCounters, \@counters)) {
            print STDERR "Err: # of counter paths for $sampleDate is different.\n";
         }
         else {
            printValues($sampleDate, @values);   
         }
         # now start a new sample
         ++$sampleCount;
         $sampleDate = $rowDate;
         @counters   = ($counterPath);
         @values     = ($value);
      }
   }
}
# if it's still the 1st sample, we have to print the header
printHeader($timeColumnHeader, @counters) if $sampleCount == 1;

# print the last sample
if (dbaSetDiff(\@counters, \@theCounters) or 
    dbaSetDiff(\@theCounters, \@counters)) {
   print STDERR "Err: # of counter paths for $sampleDate is different.\n";
}
else {
   printValues($sampleDate, @values);   
}
exit;

##################
sub printHeader {
   my ($timeColumnHeader, @counters) = @_;
   print "\"$timeColumnHeader\"";
   foreach my $col (@counters) {
      print ",\"$col\"";
   }
   print "\n";
} # printheader

##################
sub printValues {
   my ($sampleDate, @values) = @_;
   print "\"$sampleDate\"";
   foreach my $value (@values) {
      print ",\"$value\"";
   }
   print "\n";
} # printValues


__END__

=head1 NAME

createPerfLog - create a Perfmon log file in the CSV format

=head1 SYNOPSIS

 cmd>perl createPerfLog.pl <Fixed Column Data file> > <Perfmon Log CSV file>

=head1 DESCRIPTION

This script takes a fixed column data file and creates a Perfmon log file in the CSV format. 
This is essentially the reverse of the script preparePerfLog.pl in Lisitng 4-12.

Note that the Perfmon log file in the CSV format is not suitable for bulk copy 
because (1) its columns vary when the Perfmon counters are added and dropped, and (2)
they are usually very wide.

The script expects the structure of the data file to be consistent with the following table:

 CREATE TABLE tbSysperf (
       LogTime       datetime        NOT NULL ,
       Server        varchar (128)   NOT NULL ,
       Object        varchar (128)   NOT NULL ,
       Instance      varchar (128)   NULL ,
       Counter       varchar (128)   NOT NULL ,
       Value         float           NULL
 )

For example, the following tab-separated sample data is acceptable:

 09/08/2002 12:35:45.166 SQL01   Memory      Pages/sec   11.296813511113458
 09/08/2002 12:35:45.166 SQL01   PhysicalDisk   0 D:  % Disk Time 7.0625001143654654e
 09/08/2002 12:36:00.167 SQL01   Memory      Pages/sec   0.26679653905997197
 09/08/2002 12:36:00.167 SQL01   PhysicalDisk   0 D:  % Disk Time 2.8829087316668804
 09/08/2002 12:36:15.169 SQL01   Memory      Pages/sec   0.13331924737007395
 09/08/2002 12:36:15.169 SQL01   PhysicalDisk   0 D:  % Disk Time 2.0239260004978679

The task of the script createPerfLog.pl is to convert data in this format into the CSV format used by
the Windows performance counter log, which is described next.

=head1 STRUCTURE OF THE GENERATED PERFMON LOGS

The structure of the data generated by the script createPerfLog.pl is exactly the same as 
the Windows performance counter log as created by the Performance Logs 
and Alerts tool of Windows 2000. To keep the discussion brief, assume that you have logged 
two counters on the server SQL01: 

 Memory\Pages/sec 
 PhysicalDisk(0 D:)\% Disk Time
 
In the counter log, each counter is identified by what is known as the counter path, which is a 
combination of computer name, performance object, performance instance, instance index, and 
performance counter. A counter path typically follows this format:

 \\Computer_name\Object_name(Instance_name#Index_Number)\Counter_name
 
Using this notation of the counter path, your two performance counters are identified as 
follows in the counter log:

 \\SQL01\Memory\Pages/sec
 \\SQL01\PhysicalDisk(0 D:)\% Disk Time

The entries of the counter log in the CSV file format should look like the following, using the
same sample data:

 "(PDH-CSV 4.0) (Eastern Daylight Time)(240)","\\SQL01\Memory\Pages/sec","\\SQL01\PhysicalDisk(0 D:)\% Disk Time"
 "09/08/2002 12:35:45.166","11.296813511113458","7.0625001143654654e"
 "09/08/2002 12:36:00.167","0.26679653905997197","2.8829087316668804"
 "09/08/2002 12:36:15.169","0.13331924737007395","2.0239260004978679"

You can then use the familiar Performance Monitor to plot the performance charts.

=head1 TESTS

Two sample data files are included in the Listing_4-14 folder. These two data files are the same as 
the ones generated by the script preparePerfLog.pl in the Listing_4-12 folder. The two files are:

 test1_prepared.txt
 test2_prepared.txt

To test the script createPerfLog.pl, open an NT command prompt, make the Listing_4-14 folder your 
current directory, and run the following:

 cmd>perl createPerfLog.pl test1_prepared.txt > test1.csv
 cmd>perl createPerfLog.pl test2_prepared.txt > test2.csv

The two CSV files should be exactly the same as the corresponding ones in the Listing_4-12 folder. 

=head1 AUTHOR

Linchi Shea

=head1 VERSION

 2002.12.30

=cut
